apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
    }
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/public"
    }

    maven {
        credentials {
            if(System.getenv('USERNAME') != null) {
                username  System.getenv('USERNAME')
            }
            else username "${USERNAME}"

            if(System.getenv('TOKEN') != null) {
                password  System.getenv('TOKEN')
            }
            else password "${TOKEN}"
        }
        name = "HydraConnector_maven"
        url = "https://maven.pkg.github.com/FistinNetwork/HydraConnector"
    }
}

configurations {
    shade
    implementation.extendsFrom shade
}

shadowJar {
    configurations = [project.configurations.shade]
}

dependencies {
    implementation 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'

    shade project.parent
    shade 'fr.flowarg:sch:1.0.1'
    shade ('fr.fistin:hydra-connector:1.1.5-SNAPSHOT') {
        exclude module: 'gson'
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
    from project.parent.sourceSets.main.allSource
}

jar {
    from {
        project.parent.sourceSets.main.output
    }
}

artifacts {
    archives sourcesJar, javadocJar, shadowJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar
            artifact tasks.shadowJar

            pom {
                groupId = project.group
                artifactId = 'fistinapi'
                version = project.version
                name = 'FistinAPI'
                description = 'API afin de communiquer entre les diff√©rents composants de l\'infrastructure de FistinNetwork. (Impl)'
                url = 'https://github.com/FistinNetwork/FistinAPI'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('USERNAME')
                password System.getenv('TOKEN')
            }
            name "FistinAPI_maven"
            url 'https://maven.pkg.github.com/FistinNetwork/FistinAPI'
        }
    }
}
