plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'idea'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

group 'fr.fistin'
version '1.5.0'
archivesBaseName = "FistinAPI"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

compileJava {
    targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

repositories {
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
    }
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/public"
    }
    mavenCentral()
    jcenter()
}

configurations {
    shade
}

shadowJar {
    configurations = [project.configurations.shade]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

task apiJar(type: Jar) {
    classifier 'api'
    from sourceSets.main.output
    include 'fr/fistin/api/**'
    exclude 'fr/fistin/api/impl/**'
}

artifacts {
    archives sourcesJar, shadowJar, javadocJar, apiJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.shadowJar
            artifact tasks.javadocJar
            artifact tasks.apiJar

            pom {
                groupId = project.group
                artifactId = 'fistinapi'
                version = project.version
                name = 'FistinAPI'
                description = 'An API for FistinNetwork, including some interesting features.'
                url = 'https://github.com/FistinNetwork/FistinAPI'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('USERNAME')
                password System.getenv('TOKEN')
            }
            name = "FistinAPI_maven"
            url = "https://maven.pkg.github.com/FistinNetwork/FistinAPI"
        }
    }
}

dependencies {
    api 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    api 'fr.flowarg:sch:1.0.1'
    api 'org.jetbrains:annotations:20.1.0'
    shade 'fr.flowarg:sch:1.0.1'
}
