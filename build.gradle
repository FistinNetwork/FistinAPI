plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'idea'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

group 'fr.flowarg.fistinnetwork'
version '1.1.0'
archivesBaseName = "FistinAPI"

compileJava {
    targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

repositories {
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
    }
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/public"
    }
    maven {
        url "https://flowarg.github.io/maven/"
    }
    jcenter()
    mavenCentral()
}

shadowJar {
    configurations = [project.configurations.shadow]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar, shadowJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.shadowJar
            
            pom {
                groupId = project.group
                artifactId = 'fistinapi'
                version = project.version
                name = 'FistinAPI'
                description = 'FistinAPI'
                url = 'https://github.com/FistinNetwork/FistinAPI'
            }
        }
    }
        
    repositories {
        maven {
            credentials {
                username System.getenv('USERNAME')
                password System.getenv('TOKEN')
            }
            name = "FistinAPI_maven"
            url = "https://maven.pkg.github.com/FistinNetwork/FistinAPI"
        }
    }
}

dependencies {
	implementation 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    compileOnly fileTree(dir: 'libs', include: '*.jar')
    shadow 'fr.flowarg:sch:1.0.0'
}
