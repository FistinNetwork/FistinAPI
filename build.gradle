plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group 'fr.fistin'
    version '1.7.1'

    java {
        withJavadocJar()
        withSourcesJar()

        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    compileJava {
        targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_8
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()

        maven {
            credentials {
                if (System.getenv('USERNAME') != null) {
                    username System.getenv('USERNAME')
                }
                else username "${USERNAME}"

                if (System.getenv('TOKEN') != null) {
                    password System.getenv('TOKEN')
                }
                else password "${TOKEN}"
            }

            name = "Hydra_maven"
            url = "https://maven.pkg.github.com/FistinNetwork/Hydra"
        }
    }
}

archivesBaseName = "FistinAPI"

configurations {
    shade
    api.extendsFrom shade
}

shadowJar {
    configurations = [project.configurations.shade]
}

dependencies {
    shade 'fr.fistin:hydra-api:1.0.0'

    api 'org.jetbrains:annotations:23.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'org.mockito:mockito-core:4.8.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.8.0'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            pom {
                groupId = project.group
                artifactId = 'fistinapi-api'
                version = project.version
                name = 'FistinAPI'
                description = 'API afin de communiquer entre les diff√©rents composants de l\'infrastructure de FistinNetwork. (API)'
                url = 'https://github.com/FistinNetwork/FistinAPI'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('USERNAME')
                password System.getenv('TOKEN')
            }
            name "FistinAPI_maven"
            url "https://maven.pkg.github.com/FistinNetwork/FistinAPI"
        }
    }
}
