plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0' apply false
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group 'fr.fistin'
    version '1.6.1'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    compileJava {
        targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_8
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
    }

    task javadocJar(type: Jar) {
        classifier 'javadoc'
        from javadoc
    }
}

archivesBaseName = "FistinAPI"

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            pom {
                groupId = project.group
                artifactId = 'fistinapi-api'
                version = project.version
                name = 'FistinAPI'
                description = 'API afin de communiquer entre les diff√©rents composants de l\'infrastructure de FistinNetwork. (API)'
                url = 'https://github.com/FistinNetwork/FistinAPI'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('USERNAME')
                password System.getenv('TOKEN')
            }
            name "FistinAPI_maven"
            url "https://maven.pkg.github.com/FistinNetwork/FistinAPI"
        }
    }
}

dependencies {
    api 'org.jetbrains:annotations:22.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation 'org.mockito:mockito-core:3.12.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.12.1'
}

test {
    useJUnitPlatform()
}
