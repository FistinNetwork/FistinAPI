plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0' apply false
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group 'fr.fistin'
    version '1.6.0'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    compileJava {
        targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_8
        options.encoding = 'UTF-8'
    }

    repositories {
        maven {
            credentials {
                username System.getenv('USERNAME') != null ? System.getenv('USERNAME') : "${USERNAME}"
                password System.getenv('TOKEN') != null ? System.getenv('TOKEN') : "${TOKEN}"
            }
            name = "HydraConnector_maven"
            url = "https://maven.pkg.github.com/FistinNetwork/HydraConnector"
        }

        mavenCentral()
    }

    task javadocJar(type: Jar) {
        classifier 'javadoc'
        from javadoc
    }
}

archivesBaseName = "FistinAPI"

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task apiJar(type: Jar) {
    classifier 'api'
    from sourceSets.main.output
    include 'fr/fistin/api/**'
    exclude 'fr/fistin/api/impl/**'
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            pom {
                groupId = project.group
                artifactId = 'fistinapi'
                version = project.version
                name = 'FistinAPI'
                description = 'API afin de communiquer entre les diff√©rents composants de l\'infrastructure de FistinNetwork. (API)'
                url = 'https://github.com/FistinNetwork/FistinAPI'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('USERNAME')
                password System.getenv('TOKEN')
            }
            name "FistinAPI_maven"
            url "https://maven.pkg.github.com/FistinNetwork/FistinAPI"
        }
    }
}

dependencies {
    api 'org.jetbrains:annotations:20.1.0'
    api 'fr.fistin:hydra-connector:1.1.4-SNAPSHOT'
    testImplementation 'junit:junit:4.13.2'
}
